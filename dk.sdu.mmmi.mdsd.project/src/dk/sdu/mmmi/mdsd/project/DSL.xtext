grammar dk.sdu.mmmi.mdsd.project.DSL with org.eclipse.xtext.common.Terminals

generate dSL "http://www.sdu.dk/mmmi/mdsd/project/DSL"

RobotDefinition:
	'RobotDefinition' 'for' name=ID area=Area tasks+=Task+ robots+=Robot+
;

Area:
	'area' name=ID 'size' size=Vector2 items+=AreaItem* 'endarea'
;

Vector2:
	x=INT y=INT
;

AreaItem:
	Pickupable | Obstacle
;

Pickupable:
	'pickupable' name=ID 'pos' pos=Vector2 properties+=Property* 'endpickupable'
;

Obstacle:
	'obstacle' name=ID 'pos' pos=Vector2 'size' size=Vector2 'endobstacle'
;

Property:
	'property' name=ID 'default' default=INT
;

Task:
	'task' name=ID items+=TaskItem* 'endtask'
;

TaskItem:
	Action | Condition
;

Action:
	{Pickup} 'pickup' 
	| {Setdown} 'setdown'
	| {Forward} 'forward' amount=INT
	| {Backward} 'backward' amount=INT
	| {Turn} 'turn' direction=Direction
	| {Retry} 'retry'
	| {DoTask} 'do' task=[Task]
;

Direction:
	{LeftDir} 'left' | {RightDir} 'right'
;

Condition:
	'if' state=State tasks+=TaskItem* else=Else? 'endif'
;

State returns Expression:
	{StateAt} 'at' pickupable=[Pickupable]
	| {StatePickedUp} 'picked' 'up' prop=[Property] sign=Sign right=Exp
	| Exp {State.left = current} sign=Sign right=Exp
;

Sign:
	{Equals} '='
	| {SmallerThan} '<'
	| {GreaterThan} '>'
	| {SmallerThanEquals} '<='
	| {GreaterThanEquals} '>='
;

Else:
	{Else} 'else' tasks+=TaskItem*
;

Exp returns Expression:
	Factor (('+' {Plus.left = current} | '-' {Minus.left = current}) right=Factor)* 
;

Factor returns Expression:
	Primary (('*' {Mult.left = current} | '/' {Div.left = current}) right=Primary)*
;

Primary returns Expression:
	Number | Parentheses | StateVars
;

Parentheses returns Expression:
	'(' Exp ')'
;

Number returns Expression:
	{Num} value=INT
;

StateVars returns Expression:
	{StateRetries} 'retries'
;

Robot:
	'robot' name=ID startpoint=StartPoint path=Path mission=Mission 'endrobot'
;

StartPoint:
	'startpoint' pos=Vector2
;

Path:
	{Path} 'path' items+=TaskItem* 'endpath'
;

Mission:
	{Mission} 'mission' triggers+=WhenTrigger* 'endmission'
;

MissionTriggerAction:
	WhenTrigger | WaitAction | TaskItem
;

WhenTrigger:
	'when' 'at' trigger=WhenAtTrigger items+=MissionTriggerAction* 'end'
;

WhenAtTrigger:
	{WhenAtPickupable} 'pickupable' pickupable=[Pickupable]
	| {WhenAtPos} 'pos' pos=Vector2
;

WaitAction:
	'wait' (ForTime | 'until' Until)
;

ForTime:
	'for' time=Time
;

Until:
	({UntilRobot} 'robot' robot=[Robot] | {UntilPickupable} 'pickupable' pickupable=[Pickupable])
	'at' pos=Vector2 (time=ForTime 'or' or=Or)?
;

Time:
	{Seconds} time=INT 'seconds'
	| {Minutes} time=INT 'minutes'
;

Or:
	{Cancel} 'cancel'
	| {Terminate} 'terminate'
;












